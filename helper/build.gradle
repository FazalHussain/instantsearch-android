plugins {
    id 'com.android.library'
    id 'kotlin-multiplatform' version '1.3.30-eap-45'
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            java.srcDirs = ['src/androidMain/kotlin']
            res.srcDirs = ['src/androidMain/res']
        }
        test {
            java.srcDirs = ['src/androidTest/kotlin']
            res.srcDirs = ['src/androidTest/res']
        }
    }
}

configurations {
    compileClasspath //FIXME: Workaround for https://youtrack.jetbrains.com/issue/KT-27170
}

kotlin {
    android()
    jvm {
        compilations.main.kotlinOptions {
            jvmTarget = "1.8"
        }
        compilations.test.kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    sourceSets {
        commonMain.dependencies {
            api project(':core')
            implementation "com.algolia:algoliasearch-client-kotlin-common:1.0.0"
            implementation kotlin('stdlib-common')
        }
        commonTest.dependencies {
            implementation kotlin('test-common')
            implementation kotlin('test-annotations-common')
            implementation "io.ktor:ktor-client-mock:1.2.0-alpha-1.3.0-eap-125"
        }
        jvmMain.dependencies {
            implementation "com.algolia:algoliasearch-client-kotlin-jvm:1.0.0"
            implementation "io.ktor:ktor-client-okhttp:1.2.0-alpha-1.3.0-eap-125"
            implementation kotlin('stdlib-jdk8')
        }
        jvmTest.dependencies {
            implementation kotlin('test')
            implementation kotlin('test-junit')
            implementation 'org.slf4j:slf4j-simple:1.7.5'
            implementation "io.ktor:ktor-client-mock-jvm:1.2.0-alpha-1.3.0-eap-125"
        }
        androidMain.dependencies {
            implementation 'com.android.support:appcompat-v7:28.0.0'
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.0'
            api "com.algolia:algoliasearch-client-kotlin-jvm:1.0.0"

//            TODO: Add RecyclerView when moving widgets to helper from sample
            // implementation 'androidx.recyclerview:recyclerview:1.0.0'
        }
        androidTest.dependencies {
            implementation project(":core")
            implementation "com.algolia:algoliasearch-client-kotlin-jvm:1.0.0"
            implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.21'
            implementation 'org.jetbrains.kotlin:kotlin-test-junit:1.3.21'
            implementation 'junit:junit:4.12'
            implementation 'com.android.support.test:runner:1.0.2'
            implementation 'com.android.support.test.espresso:espresso-core:3.0.2'
        }
    }
}
